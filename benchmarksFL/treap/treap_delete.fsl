(Function key   Loc Int)
(Function prio  Loc Int)
(Function left  Loc Loc)
(Function right Loc Loc)

(EqSp (Keys (MinKey MaxKey MaxPrio Treap Priorities)))


(RecFunction Keys       Loc SetInt)
(RecFunction MinKey  Loc Int)
(RecFunction MaxKey  Loc Int)
(RecFunction MaxPrio Loc Int)
(RecFunction Treap      Loc Bool)
(RecFunction Priorities Loc SetInt)

(Var x Loc)
(Var plus_infty Int)
(Var minus_infty Int)

(RecDef (MinKey x)
  (ite (= x nil) plus_infty
    (ite (<= (key x) (MinKey (left x)))
      (ite (<= (key x) (MinKey (right x)))
        (key x)
        (MinKey (right x))
      )
      (ite (<= (MinKey (left x)) (MinKey (right x)))
        (MinKey (left x))
        (MinKey (right x))))))

(RecDef (MaxKey x)
  (ite (= x nil) minus_infty
    (ite (>= (key x) (MaxKey (left x)))
      (ite (>= (key x) (MaxKey (right x)))
        (key x)
        (MaxKey (right x))
      )
      (ite (>= (MaxKey (left x)) (MaxKey (right x)))
        (MaxKey (left x))
        (MaxKey (right x))))))
(RecDef (MaxPrio x)
  (ite (= x nil) minus_infty
    (ite (>= (prio x) (MaxPrio (left x)))
      (ite (>= (prio x) (MaxPrio (right x)))
        (prio x)
        (MaxPrio (right x))
      )
      (ite (>= (MaxPrio (left x)) (MaxPrio (right x)))
        (MaxPrio (left x))
        (MaxPrio (right x))))))

(RecDef (Treap x)
  (ite (= x nil) True
    (and (< minus_infty (key x)) (< (key x) plus_infty)
    (< minus_infty (prio x)) (< (prio x) plus_infty)
    (Treap (left x))  (< (MaxKey (left x)) (key x))  (< (MaxPrio (left x)) (prio x))
         (Treap (right x)) (< (key x) (MinKey (right x))) (< (MaxPrio (right x)) (prio x))
         (= EmptySetInt (SetIntersect (Priorities (left x)) (Priorities (right x))))
         (not (IsMember x (Sp (Treap (antiSp (left x))))))
         (not (IsMember x (Sp (Treap (antiSp (right x))))))
         (= EmptySetLoc (SetIntersect (Sp (Treap (antiSp (left x))))
                                      (Sp (Treap (antiSp (right x))))))
         )))

(RecDef (Keys x)
  (ite (= x nil) EmptySetInt
    (SetAdd (SetUnion (Keys (left x)) (Keys (right x))) (key x))))
(RecDef (Priorities x)
  (ite (= x nil) EmptySetInt
    (SetAdd (SetUnion (Priorities (left x)) (Priorities (right x))) (prio x))))

(Var k Int)
(Var p Int)
(Var ret Loc)
(Var rht Loc)
(Var lft Loc)
(Var tmp Loc)

(Var right_left Loc)
(Var right_right Loc)
(Var left_left Loc)
(Var left_right Loc)

(Var xl Loc)
(Var xr Loc)

(lemma (x) (=> (Treap x) (=> (< k (MinKey x)) (not (IsMember k (Keys x))))))
(lemma (x) (=> (Treap x) (=> (> k (MaxKey x)) (not (IsMember k (Keys x))))))
(lemma (x) (=> (Treap x) (=> (> p (MaxPrio x)) (not (IsMember p (Priorities x))))))

(lemma (x) (=> (Treap x) (=> (not (= x nil)) (= (MaxPrio x) (prio x)))))

(Program treap_remove_root (x k p) (ret))
(Pre (and (not (= x nil)) (Treap x) (= (key x) k) (= (prio x) p) ))
(Post (and (Treap ret) (= (Keys ret) (SetDel (Old (Keys x)) k))
           (= (Priorities ret) (SetDel (Old (Priorities x)) p))
           (<= (Old (MinKey x)) (MinKey ret)) (<= (MaxKey ret) (Old (MaxKey x)))
           (ite (<= (Old (MaxPrio (left x))) (Old (MaxPrio (right x))))
              (= (MaxPrio ret) (Old (MaxPrio (right x))))
              (= (MaxPrio ret) (Old (MaxPrio (left x)))))
           (ite True True (and (= (left ret) (left ret))
                               (= (right ret) (right ret)))) ))

(If (and (= (left x) nil) (= (right x) nil))
 Then
  (free x)
  (assign ret nil)
  (return)
 Else (If (= (left x) nil)
 Then
  (assume (<= minus_infty (prio (right x))))
  (assign ret (right x))
  (free x)
  (return)
 Else (If (= (right x) nil)
 Then
  (assume (<= minus_infty (prio (left x))))
  (assign ret (left x))
  (free x)
  (return)
 Else
  (assign rht (right x))
  (assign lft (left x))

  (If (<= (prio lft) (prio rht))
   Then
    (assign right_left (left rht))
    (assign (right x) right_left)
    (call treap_remove_root (x k p) (tmp))
    (assign (left rht) tmp)
    (assign ret rht)
    (return)
   Else
    (assign left_right (right lft))
    (assign (left x) left_right)
    (call treap_remove_root (x k p) (tmp))
    (assign (right lft) tmp)
    (assign ret lft)
    (return)
  )
)))

(Program treap_delete (x k) (ret))
(Pre (and (Treap x) (< minus_infty k) (< k plus_infty)) )
(Post (and (Treap ret) (= (Keys ret) (SetDel (Old (Keys x)) k))
           (IsSubset (Priorities ret) (Old (Priorities x)))
           (<= (Old (MinKey x)) (MinKey ret)) (<= (MaxKey ret) (Old (MaxKey x)))
           (<= (MaxPrio ret) (Old (MaxPrio x))) ))

(If (= x nil)
 Then
  (assign ret x)
  (return)
 Else (If (= (key x) k)
 Then
  (assign p (prio x))
  (call treap_remove_root (x k p) (ret))
  (return)
 Else (If (< k (key x))
 Then
  (assign xl (left x))
  (call treap_delete (xl k) (tmp))
  (assign (left x) tmp)
  (assign ret x)
  (return)
 Else
  (assign xr (right x))
  (call treap_delete (xr k) (tmp))
  (assign (right x) tmp)
  (assign ret x)
  (return)
)))
