(Var x Loc)
(Var k Int)
(Var nxt Loc)

(Function next Loc Loc)
(Function key  Loc Int)

(Var plus_infty Int)

(EqSp (Min (List Keys Sorted)))
(RecFunction Min Loc Int)
(RecDef (Min x) (ite (= x nil) plus_infty (ite (< (key x) (Min (next x))) (key x) (Min (next x)))))

(RecFunction Keys   Loc SetInt)
(RecFunction Sorted Loc Bool)

(RecDef (Keys x) (ite (= x nil) EmptySetInt
                 (SetAdd (Keys (next x)) (key x))))

(RecDef (Sorted x) (ite (= x nil) True
                    (Exists (= nxt (next x)) (Exists (= k (key x))
                    (* (< k plus_infty) (* 
                      (= k (key x)) (and (Sorted nxt) (<= k (Min nxt)))))
                    ))
               ))

(Var ret Loc)
(Var tmp Loc)

(Program sorted_delete (x k) (ret))
(Pre (Sorted x))
(Post (and (Sorted ret) (and (= (Keys ret) (SetDel (Old (Keys x)) k))
           (<= (Old (Min x)) (Min ret)) ) ))

(If (= x nil)
 Then
  (assign ret x)
  (return)
 Else (If (= (key x) k)
 Then
  (assign nxt (next x))
  (call sorted_delete (nxt k) (ret))
  (free x)
  (return)
 Else
  (assign nxt (next x))
  (call sorted_delete (nxt k) (tmp))
  (assign (next x) tmp)
  (assign ret x)
  (return)
))
