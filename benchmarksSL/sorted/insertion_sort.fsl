(Var x Loc)
(Var k Int)
(Var nxt Loc)

(Function next Loc Loc)
(Function key  Loc Int)

(EqSp (List (Min Keys Sorted)))
(Var plus_infty Int)

(RecFunction List   Loc Bool)
(RecFunction Min Loc Int)
(RecFunction Keys   Loc SetInt)
(RecFunction Sorted Loc Bool)

(RecDef (Min x) (ite (= x nil) plus_infty (ite (< (key x) (Min (next x))) (key x) (Min (next x)))))



(RecDef (List x) (ite (= x nil) True
                    (Exists (= y (next x)) (* (= (next x) (next x)) (List y)))
               ))

(RecDef (Keys x) (ite (= x nil) EmptySetInt
                 (SetAdd (Keys (next x)) (key x))))

(RecDef (Sorted x) (ite (= x nil) True
                    (Exists (= nxt (next x)) (Exists (= k (key x))
                    (*  (< k plus_infty) (* 
                      (= k (key x)) (and (Sorted nxt) (<= k (Min nxt)))))
                    ))
               ))


(lemma (x) (=> (Sorted x) (List x)))

(Var y Loc)
(Var ret Loc)
(Var tmp Loc)

(Program sorted_insert (x) (ret))
(Pre (* (not (= x nil)) (and (List x) (Sorted (next x))) ))
(Post (and (Sorted ret) (and (= (Keys ret) (Old (Keys x)))
           (= (Min ret) (Old (Min x))) )))

(assign nxt (next x))
(If (= nxt nil)
 Then
  (assume (< (key x) plus_infty))
  (assign ret x)
  (return)
 Else (If (> (key x) (key nxt))
 Then
  (assign (next x) (next nxt))
  (call sorted_insert (x) (tmp))
  (assign (next nxt) tmp)
  (assign ret nxt)
  (return)
 Else
  (assign ret x)
  (return)
))

(Program insertion_sort (x) (ret))
(Pre (List x))
(Post (and (Sorted ret) (= (Keys ret) (Old (Keys x))) ))

(If (= x nil)
 Then
  (assign ret x)
  (return)
 Else
  (assign nxt (next x))
  (call insertion_sort (nxt) (tmp))
  (assign (next x) tmp)
  (call sorted_insert (x) (ret))
  (return)
)
